# Brief: Builds firmware source generated by STM32CubeMX.
#
# Author:  Windsor Schmidt. | https://github.com/windsorschmidt
# License: MIT License. Contributions welcome.

cmake_minimum_required (VERSION 3.6)
set(PROJ "firmware")
project (${PROJ})

# All relative to project root, as generated by STM32CubeMX.
set(PROJECT_DIR ${CMAKE_SOURCE_DIR})
set(SOURCE_DIR ${CMAKE_SOURCE_DIR}/Src)
set(CMSIS_DIR ${CMAKE_SOURCE_DIR}/Drivers/CMSIS)
set(DEVICE_DIR ${CMAKE_SOURCE_DIR}/Drivers/CMSIS/Device/ST/STM32F1xx)
set(HAL_DIR ${CMAKE_SOURCE_DIR}/Drivers/STM32F1xx_HAL_Driver)

# Look here for header files.
include_directories(${CMAKE_SOURCE_DIR}/Inc)
include_directories(${CMSIS_DIR}/Include)
include_directories(${DEVICE_DIR}/Include)
include_directories(${HAL_DIR}/Inc)

# For diagnostic tools like rtags.
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# As of late 2016 we can get the GNU ARM Embedded Toolchain from:
# https://developer.arm.com/open-source/gnu-toolchain/gnu-rm.
set(CMAKE_C_COMPILER "arm-none-eabi-gcc")

# Some parts (e.g. Cortex-M4) have a FPU, but with F0 parts we're stuck with VFP.
set(CMAKE_C_FLAGS "-O0 -g -Wall -ffunction-sections -fdata-sections")
set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DSTM32F103xB -mcpu=cortex-m3 -mthumb")

set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -mno-thumb-interwork -mfpu=vfp -msoft-float")
set(CMAKE_EXE_LINKER_FLAGS "-T ../STM32F103C8Tx_FLASH.ld -Wl,--gc-section")

# Some trickery to get CMake to deal with our assembler code.
set_property(SOURCE ${PROJECT_DIR}/startup_stm32f103xb.s PROPERTY LANGUAGE C)

# The linker flag --gc-section keeps unused object code here from being linked.
add_executable(
  ${PROJ}.elf
  ${PROJECT_DIR}/Src/main.c
  ${PROJECT_DIR}/Src/stm32f1xx_it.c
  ${PROJECT_DIR}/Src/system_stm32f1xx.c
  ${HAL_DIR}/Src/stm32f1xx_ll_exti.c
  ${HAL_DIR}/Src/stm32f1xx_ll_gpio.c
  ${HAL_DIR}/Src/stm32f1xx_ll_pwr.c
  ${HAL_DIR}/Src/stm32f1xx_ll_rcc.c
  ${HAL_DIR}/Src/stm32f1xx_ll_utils.c
  )

# Generate assembly listing.
add_custom_command(TARGET
  ${PROJ}.elf
  COMMAND "arm-none-eabi-objdump"
  ARGS "-S" "${PROJ}.elf" ">>" "${PROJ}.lst")

# Flash firmware to target.
add_custom_target(flash
  openocd -f interface/stlink-v2.cfg -c "transport select hla_swd"
  -f target/stm32f1x.cfg -c "init" -c "reset halt"
  -c "flash write_image erase ${PROJ}.elf" -c "reset run" -c "shutdown"
  DEPENDS ${PROJ}.elf
  COMMENT "Flashing target hardware")
  
# Run OpenOCD as a GDB host.
add_custom_target(gdbhost
  openocd -f interface/stlink-v2.cfg -c "transport select hla_swd"
  -f target/stm32f1x.cfg -c "init" -c "reset halt"
  DEPENDS ${PROJ}.elf
  COMMENT "Running OpenOCD as a GDB host.")
